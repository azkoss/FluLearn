buildscript {
    repositories {
        //仓库源是从上往下寻找的，优先从阿里云镜像仓库获取
        maven {
            url 'https://maven.aliyun.com/repository/jcenter'
        }
        maven {
            url 'https://maven.aliyun.com/repository/google'
        }
        jcenter()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleBuildVersion"
    }
}

allprojects {
    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        maven {
            url 'https://maven.aliyun.com/repository/jcenter'
        }
        maven {
            url 'https://maven.aliyun.com/repository/google'
        }
        jcenter()
        google()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.configurations.all {
        //此处可用于解决依赖冲突问题，参阅 https://developer.android.google.cn/studio/build/dependencies#duplicate_classes
        resolutionStrategy.eachDependency { details ->
            // Flutter混合开发问题参阅 https://github.com/flutter/flutter/issues/27254
            if (details.requested.group == 'androidx.core') {
                details.useVersion androidxVersion
            } else if (details.requested.group == 'androidx.legacy') {
                details.useVersion androidxVersion
            } else if (details.requested.group == 'androidx.appcompat') {
                details.useVersion androidxVersion
            } else if (details.requested.group == 'androidx.arch.core') {
                details.useVersion lifecycleVersion
            }
        }
    }

    project.evaluationDependsOn(':app')
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
